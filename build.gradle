/**
 * root build script
 * https://github.com/aurmam/gradle-multimodule-app
 *
 * gradle build file genned from idea
 * setup is for spring boot add using spring initializr
 * https://www.jetbrains.com/help/idea/spring-boot.html
 * https://www.youtube.com/watch?v=he63dwZdhOM
 * https://discuss.gradle.org/t/how-to-remove-a-task-dependency/7481/7
 * https://docs.gradle.org/current/javadoc/constant-values.html#org.gradle.api.artifacts.Dependency.CLASSIFIER
 * https://stackoverflow.com/questions/44429751/how-to-use-junit-5-with-gradle
 **/
buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:2.1.3.RELEASE"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.10"
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.2.0"
        classpath 'com.kncept.junit.reporter:junit-reporter:2.0.0'
    }
}

plugins {
    id 'java'
    id 'idea'
    id "io.spring.dependency-management" version "1.0.7.RELEASE"

    // https://flywaydb.org/getstarted/firststeps/gradle
    // https://flywaydb.org/documentation/gradle/
    id 'org.flywaydb.flyway' version '5.2.4'
}

// https://flywaydb.org/documentation/gradle/
flyway {
    url = 'jdbc:mariadb://localhost'
    user = 'root'
    password = 'root'
    schemas = ['springboot_swagger']
    locations = ['filesystem:config/db/schema',
                 'filesystem:config/db/functions']
}

allprojects {
    group = 'org.tec'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        // datasource deps
        compile group: 'com.zaxxer', name: 'HikariCP', version: '3.3.0'
        compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.3.0'
    }
}

subprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    // https://stackoverflow.com/questions/44429751/how-to-use-junit-5-with-gradle
    apply plugin: 'org.junit.platform.gradle.plugin'

    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    // http://checkstyle.sourceforge.net/
    apply plugin: 'checkstyle'

    // https://pmd.github.io/
    // https://docs.gradle.org/current/userguide/pmd_plugin.html
    apply plugin: 'pmd'

    // https://www.jacoco.org/jacoco/
    // https://docs.gradle.org/current/userguide/jacoco_plugin.html
    apply plugin: 'jacoco'

    // https://plugins.gradle.org/plugin/com.github.spotbugs
    // https://spotbugs.readthedocs.io/en/stable/gradle.html
    // https://remal.gitlab.io/gradle-plugins/
    apply plugin: 'com.github.spotbugs'

    // https://github.com/kncept/junit-reporter
    apply plugin: 'com.kncept.junit.reporter'

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
    
    // had issues because of having xml libs under endorsed structure
    // https://docs.oracle.com/javase/8/docs/technotes/guides/standards/
    // https://github.com/checkstyle/contribution/tree/master/xsl
    // https://docs.gradle.org/current/userguide/checkstyle_plugin.html
    // https://stackoverflow.com/questions/32772872/how-can-i-disable-checkstyle-rules-for-tests-in-gradle
    tasks.withType(Checkstyle) {
        ignoreFailures = false
        configFile = file("${projectDir}/../config/checkstyle/checkstyle.xml")
        checkstyleTest.enabled = false
        reports {
            xml.enabled false
            html.enabled true
        }
    }

    // https://docs.gradle.org/current/dsl/org.gradle.testing.jacoco.tasks.JacocoReport.html
    // https://reflectoring.io/jacoco/
    tasks.withType(JacocoReport) {
        reports {
            html.enabled true
            xml.enabled false
            csv.enabled false
        }
    }

    // https://stackoverflow.com/questions/17237115/pmd-gradle-plugin-remove-pmdtest-upon-gradle-check
    pmd.sourceSets = [sourceSets.main]

    // https://github.com/spotbugs/spotbugs-gradle-plugin/issues/32
    // https://spotbugs.readthedocs.io/en/latest/gradle.html
    // https://spotbugs.readthedocs.io/en/latest/gradle.html#configure-gradle-plugin
    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        ignoreFailures = false
        spotbugsTest.enabled = false

        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    // https://github.com/kncept/junit-reporter
    junitHtmlReport {
        //RAG status css overrides
        cssRed = 'red'
        cssAmber = 'orange'
        cssGreen = 'green'

        //Processing directories
        // defaults work custom don't
        //testResultsDir = "${buildDir}"
        //testReportsDir = "${buildDir}/reports/tests"

        //Fail build when no XML files to process
        failOnEmpty = true
    }

    junitPlatformTest.finalizedBy 'junitHtmlReport'

    ext {
        SLF4J_VERSION = '1.7.10'
        SPRING_BOOT_VERSION='2.1.3.RELEASE'
        SPRING_BOOT_ADMIN_VERSION='2.1.3'
        LOMBOK_VERSION = '1.18.4'
    }

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:Cairo-RELEASE'
        }
    }

    dependencies {
        //https://github.com/rzwitserloot/lombok/issues/1945
        annotationProcessor("org.projectlombok:lombok:$LOMBOK_VERSION")

        // actuator for spring boot admin
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: "$SPRING_BOOT_VERSION"

        // security modules
        // https://stackoverflow.com/questions/37285016/what-is-username-and-password-when-starting-spring-boot-with-tomcat
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: "$SPRING_BOOT_VERSION"

        // datasource deps
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: "$SPRING_BOOT_VERSION"

        // logging https://www.baeldung.com/spring-boot-logging
        // logging
        //compile "org.slf4j:slf4j-api:${SLF4J_VERSION}"
        //compile "org.slf4j:slf4j-log4j12:${SLF4J_VERSION}"
        compile group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2', version: "$SPRING_BOOT_VERSION"
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.11.1'

        // utility libs
        // https://www.baeldung.com/entity-to-and-from-dto-for-a-java-spring-application
        compile group: 'org.modelmapper', name: 'modelmapper', version: '2.3.2'

        //build utilities
        // https://stackoverflow.com/questions/17729384/lombok-added-but-getters-and-setters-not-recognized-in-intellij-idea
        compileOnly group: 'org.projectlombok', name: 'lombok', version: "$LOMBOK_VERSION"

        // https://www.slf4j.org/codes.html#StaticLoggerBinder
        testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.26'

        // upgrade causes errors
        // https://stackoverflow.com/questions/53926264/nosuchmethoderror-org-junit-platform-commons-util-reflectionutils-trytoloadclas
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.2'
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.2'
        testCompile group: 'org.junit.vintage', name: 'junit-vintage-engine', version: '5.3.2'
        testRuntime group: 'com.kncept.junit.reporter', name: 'junit-reporter', version: '2.0.0'

        testCompile group: 'org.springframework', name: 'spring-test', version: '5.1.5.RELEASE'
        testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: "$SPRING_BOOT_VERSION"

        configurations {
            compile {
                //https://www.baeldung.com/spring-boot-logging
                exclude group: 'org.springframework.boot',  module: 'spring-boot-starter-logging'
                //https://stackoverflow.com/questions/51483775/debug-logging-causes-stackoverflowerror-in-spring-boot-2-0-3-release
                exclude group: 'org.slf4j' , module: 'jul-to-slf4j'
            }
        }
    }

    // https://docs.gradle.org/current/dsl/org.gradle.api.tasks.testing.Test.html 
    test {
        useJUnitPlatform()
        testLogging {
            showStandardStreams = true
        }
        reports.html.enabled = true
    }
}

/**
 * task to drop and reload the DB
 * https://docs.gradle.org/current/userguide/tutorial_using_tasks.html
 * need to quote the name to leverage dot notation
 */
task 'db.reset' {
    description 'task to drop and reload the DB'
    dependsOn flywayClean,flywayMigrate
}