plugins {
    id 'org.flywaydb.flyway' version '5.2.4'
}

version '0.0.1-SNAPSHOT'

configurations {
    db
}

// https://discuss.gradle.org/t/skipping-a-systemtest-task-for-a-specific-submodule/13121
processTestResources.enabled = false
testClasses.enabled = false
test.enabled = false
spotbugsMain.enabled = false
spotbugsTest.enabled = false
pmdTest.enabled = false
compileJava.enabled = false
compileTestJava.enabled = false
processResources.enabled = false
jar.enabled = false
bootJar.enabled = false
assemble.enabled = false
generateVersionInfo.enabled = false

dependencies {
    compile group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.3.0'

    //to get ant.sql working
    db group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '2.3.0'
}

// https://flywaydb.org/getstarted/firststeps/gradle
// https://flywaydb.org/documentation/gradle/
flyway {
    url = "jdbc:mariadb://${db_host}:${db_port}/"
    user = "${db_username}"
    password = "${db_password}"
    schemas = ["${db_name}"]
    locations = ["filesystem:$projectDir/src/main/sql/schema",
                 "filesystem:$projectDir/src/main/sql/functions"]
}

/**
 * task to drop and reload the DB
 * https://docs.gradle.org/current/userguide/tutorial_using_tasks.html
 * need to quote the name to leverage dot notation
 */
task dbReset {
    description 'task to drop and reload the DB'
    dependsOn flywayClean,flywayMigrate

    doLast {
        ant.sql(classpath: configurations.db.asPath,
                driver: 'org.mariadb.jdbc.Driver',
                url: "jdbc:mariadb://${db_host}:${db_port}/${db_name}",
                userid: "${db_username}",
                password: "${db_password}",
                delimiter: ';;',
                delimitertype: 'normal'
        ) {
            transaction {
                fileset(dir: "$projectDir/src/main/sql/data") {
                    include(name: '*.sql')
                }
            }
        }
    }
}